import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;
import java.util.Arrays;
import java.util.Set;
import java.util.HashSet;
import java.math.BigDecimal;
import ecnu.db.generator.constraintchain.arithmetic.*;
import ecnu.db.generator.constraintchain.arithmetic.operator.*;
import ecnu.db.generator.constraintchain.arithmetic.value.*;
import ecnu.db.generator.constraintchain.filter.operation.*;
import ecnu.db.generator.constraintchain.filter.logical.*;
import ecnu.db.generator.constraintchain.filter.*;
import java.io.StringReader;
import ecnu.db.schema.Schema;
import ecnu.db.utils.exception.TouchstoneException;
import ecnu.db.schema.ColumnType;
import ecnu.db.schema.ColumnManager;


/* define how to connect to the scanner! */
parser code {:
  private AndNode root = new AndNode();
  private int parameterId = 0;
  private List<Parameter> parameters;
  private Set<String> columns;
  private String tableName;
  public SelectResult parseSelectOperatorInfo(String operatorInfo) throws Exception {
    this.parameters = new ArrayList<>();
    this.columns = new HashSet<>();
    this.tableName = null;
    this.root = new AndNode();
    ((PgSelectOperatorInfoLexer) this.getScanner()).yyreset(new StringReader(operatorInfo));
    this.debug_parse();
    SelectResult result = new SelectResult();
    result.setParameters(this.parameters);
    result.setCondition(this.root);
    result.setTableName(this.tableName);
    result.setColumns(this.columns);
    return result;
  }
  private String getCanonicalTableName(String canonicalColumnName) {
    String[] names = canonicalColumnName.split("\\.");
    return String.format("%s.%s", names[0], names[1]);
  }
  private void verifyTableName(String tableName) throws TouchstoneException {
    if (this.tableName == null) {
      this.tableName = tableName;
    } else if (!this.tableName.equals(tableName)) {
      throw new TouchstoneException("select表名不一致");
    }
  }
  private float getColumnNullProbability(String tableName, String canonicalColumnName) throws TouchstoneException {
    return ColumnManager.getInstance().getColumn(canonicalColumnName.split("\\.")[2]).getNullPercentage();
  }
  private boolean isColumnNeedQuote(String canonicalColumnName) throws TouchstoneException {
    boolean needQuote = false;
    ColumnType columnType = ColumnManager.getInstance().getColumn(canonicalColumnName.split("\\.")[2]).getColumnType();
    if (columnType == ColumnType.DATETIME || columnType == ColumnType.VARCHAR) {
        needQuote = true;
    }
    return needQuote;
  }
  private boolean isColumnDate(String canonicalColumnName) throws TouchstoneException {
    ColumnType columnType = ColumnManager.getInstance().getColumn(canonicalColumnName.split("\\.")[2]).getColumnType();
    return columnType == ColumnType.DATETIME;
  }
:};

/* Terminals (tokens returned by the scanner. */
terminal   ArithmeticNodeType    PLUS, MINUS, DIV, MUL;
terminal                         LPAREN, RPAREN;
terminal   CompareOperator       ISNULL;
terminal                         NOT;
terminal   CompareOperator       IS_NOT_NULL, NOT_LIKE, NOT_IN;
terminal                         AND, OR;
terminal   CompareOperator       LT, GT, LE, GE, NE, EQ, LIKE, IN;
terminal   Integer               INTEGER;
terminal   String                STRING;
terminal   String                DATE;
terminal   String                CANONICAL_COLUMN_NAME;
terminal   Float                 FLOAT;


/* Non terminals */
non terminal                                    expr;
non terminal          List<BoolExprNode>        bool_exprs;
non terminal          BoolExprNode              bool_expr;
non terminal          BoolExprNode              logical_expr;
non terminal          List<BoolExprNode>        logical_arguments;
non terminal          AbstractFilterOperation   compare_expr;
non terminal          AbstractFilterOperation   uni_compare_expr;
non terminal          CompareOperator           uni_compare_operator;
non terminal          String                    uni_compare_constant;
non terminal          AbstractFilterOperation   multi_compare_expr;
non terminal          UniVarFilterOperation     in_compare_expr;
non terminal          String                    in_argument;
non terminal          List<String>              in_arguments;
non terminal          UniVarFilterOperation     like_compare_expr;
non terminal          ArithmeticNode            arithmetic_expr;
non terminal          ArithmeticNodeType        arithmetic_operator;
non terminal          IsNullFilterOperation     isnull_expr;
non terminal          CompareOperator           null_expr;
non terminal          AbstractFilterOperation   not_expr;
non terminal          CompareOperator           in_expr;
non terminal          CompareOperator           like_expr;

/* precedence */
precedence left         OR;
precedence left         AND;

/* start with */
start with expr;

/* ================= */
/* The grammar rules */
/* ================= */
expr ::= bool_expr:e                             {:
                                                         this.parser.root.addChild(e);
                                                 :}
        ;

bool_expr ::=
        logical_expr:e                           {:  RESULT = e;  :}
        | compare_expr:e                         {:  RESULT = e;  :}
        | isnull_expr:e                          {:  RESULT = e;  :}
        | not_expr:e                             {:  RESULT = e;  :}
        ;

/* logical expressions */

logical_expr ::=
        LPAREN bool_expr:e1 OR:or bool_expr:e2 RPAREN       {:
                                                                OrNode node = new OrNode();
                                                                node.setLeftNode(e1);
                                                                node.setRightNode(e2);
                                                                RESULT = node;
                                                            :}
        | LPAREN logical_arguments:children RPAREN          {:
                                                                AndNode node = new AndNode();
                                                                for (BoolExprNode child: children) {
                                                                    node.addChild(child);
                                                                }
                                                                RESULT = node;
                                                            :}
        ;
logical_arguments ::=
        bool_expr:e1 AND:and bool_expr:e2                   {:  RESULT = new ArrayList<>(Arrays.asList(e1, e2));  :}
        ;


/* compare expressions */

compare_expr ::=
        uni_compare_expr:e                                   {:  RESULT=e;  :}
        | multi_compare_expr:e                               {:  RESULT=e;  :}
        ;
uni_compare_expr ::=
        LPAREN arithmetic_expr:e1 uni_compare_operator:op uni_compare_constant:constant RPAREN  {:
                                                                                                    AbstractFilterOperation operation;
                                                                                                    boolean needQuote = false, isDate = false;
                                                                                                    if (e1.getType() == ArithmeticNodeType.COLUMN) {
                                                                                                        String canonicalColumnName = String.format("%s.%s", tableName, ((ColumnNode) e1).getCanonicalColumnName());
                                                                                                        operation = new UniVarFilterOperation(canonicalColumnName, op);
                                                                                                        needQuote = isColumnNeedQuote(canonicalColumnName);
                                                                                                        isDate = isColumnDate(canonicalColumnName);
                                                                                                        columns.add(canonicalColumnName);
                                                                                                    } else {
                                                                                                        operation = new MultiVarFilterOperation(op, e1);
                                                                                                    }
                                                                                                    Parameter parameter = new Parameter(parameterId++, e1.toString(), constant);
                                                                                                    operation.addParameter(parameter);
                                                                                                    parameters.add(parameter);
                                                                                                    RESULT = operation;
                                                                                                :}
        ;
uni_compare_operator ::=
        LT:lt                                                    {:  RESULT = lt;  :}
        | GT:gt                                                  {:  RESULT = gt;  :}
        | LE:le                                                  {:  RESULT = le;  :}
        | GE:ge                                                  {:  RESULT = ge;  :}
        | EQ:eq                                                  {:  RESULT = eq;  :}
        | NE:ne                                                  {:  RESULT = ne;  :}
        ;
uni_compare_constant ::=
        INTEGER:num                                              {:  RESULT = num.toString();  :}
        | FLOAT:num                                              {:  RESULT = num.toString();  :}
        | DATE:date                                              {:  RESULT = date;  :}
        | STRING:str                                             {:  RESULT = str; :}
        ;
multi_compare_expr ::=
        in_compare_expr:e                                        {:  RESULT=e;  :}
        | like_compare_expr:e                                    {:  RESULT=e;  :}
        ;
in_compare_expr ::=
        LPAREN LPAREN CANONICAL_COLUMN_NAME:col RPAREN in_expr:inornotin LPAREN in_arguments:arguments RPAREN RPAREN      {:
                                                                                      String tableName = getCanonicalTableName(col);
                                                                                      verifyTableName(tableName);
                                                                                      UniVarFilterOperation operation = new UniVarFilterOperation(col, inornotin);
                                                                                      boolean needQuote = isColumnNeedQuote(col), isDate = isColumnDate(col);
                                                                                      for (String argument: arguments) {
                                                                                        Parameter parameter = new Parameter(parameterId++, col, argument);
                                                                                        operation.addParameter(parameter);
                                                                                        parameters.add(parameter);
                                                                                      }
                                                                                      columns.add(col);
                                                                                      if(inornotin == CompareOperator.NOT_IN){
                                                                                        operation.setHasNot(true);
                                                                                      }
                                                                                      RESULT = operation;
                                                                                  :}
        ;
in_expr ::=
        NOT_IN:notin                                                              {: RESULT = notin; :}
        | IN:in                                                                   {: RESULT = in; :}
        ;
in_argument  ::=
        STRING:str                                                                {:
                                                                                      RESULT = str;
                                                                                  :}
        | DATE:date                                                               {:
                                                                                      RESULT = date;
                                                                                  :}
        | INTEGER:integer                                                         {:
                                                                                      RESULT = integer.toString();
                                                                                  :}
        | FLOAT:float_                                                            {:
                                                                                      RESULT = float_.toString();
                                                                                  :}
        ;
in_arguments ::=
        in_argument:argument                                                      {:
                                                                                      List<String> arguments = new ArrayList<>();
                                                                                      arguments.add(argument);
                                                                                      RESULT = arguments;
                                                                                  :}
        | in_arguments:e1 in_argument:argument                                    {:
                                                                                      e1.add(argument);
                                                                                      RESULT = e1;
                                                                                  :}
        ;
like_compare_expr ::=
        LPAREN LPAREN CANONICAL_COLUMN_NAME:col RPAREN like_expr:likeornot STRING:str RPAREN     {:
                                                                                            String tableName = getCanonicalTableName(col);
                                                                                            verifyTableName(tableName);
                                                                                            UniVarFilterOperation operation = new UniVarFilterOperation(col, likeornot);
                                                                                            Parameter parameter = new Parameter(parameterId++, col, str);
                                                                                            operation.addParameter(parameter);
                                                                                            parameters.add(parameter);
                                                                                            columns.add(col);
                                                                                            if(likeornot == CompareOperator.NOT_LIKE){
                                                                                              operation.setHasNot(true);
                                                                                            }
                                                                                            RESULT = operation;
                                                                                       :}
        ;
like_expr ::=
        NOT_LIKE:notlike                                                               {: RESULT = notlike; :}
        | LIKE:like                                                                    {: RESULT = like; :}
        ;

/* arithmetic expressions */
arithmetic_expr ::=
        LPAREN arithmetic_expr:e1 arithmetic_operator:op arithmetic_expr:e2 RPAREN            {:
                                                                                                  ArithmeticNode node = ArithmeticNodeFactory.create(op);
                                                                                                  node.setLeftNode(e1);
                                                                                                  node.setRightNode(e2);
                                                                                                  RESULT = node;
                                                                                              :}
        | CANONICAL_COLUMN_NAME:col                                               {:
                                                                                      String tableName = getCanonicalTableName(col);
                                                                                      verifyTableName(tableName);
                                                                                      ArithmeticNode node = ArithmeticNodeFactory.create(ArithmeticNodeType.COLUMN);
                                                                                      ((ColumnNode) node).setCanonicalColumnName(col);
                                                                                      columns.add(col);
                                                                                      RESULT = node;
                                                                                  :}
        | INTEGER:num                                                             {:
                                                                                      ArithmeticNode node = ArithmeticNodeFactory.create(ArithmeticNodeType.CONSTANT);
                                                                                      ((NumericNode) node).setConstant(num);
                                                                                      RESULT = node;
                                                                                  :}
        | FLOAT:num                                                               {:
                                                                                      ArithmeticNode node = ArithmeticNodeFactory.create(ArithmeticNodeType.CONSTANT);
                                                                                      ((NumericNode) node).setConstant(num);
                                                                                      RESULT = node;
                                                                                  :}
        ;
arithmetic_operator ::=
        PLUS:plus                                                                 {:  RESULT = plus;  :}
        | MINUS:minus                                                             {:  RESULT = minus;  :}
        | MUL:mul                                                                 {:  RESULT = mul;  :}
        | DIV:div                                                                 {:  RESULT = div;  :}
        ;

/* isnull expressions */
isnull_expr ::=
        LPAREN CANONICAL_COLUMN_NAME:col null_expr:isornotnull RPAREN             {:
                                                                                      String tableName = getCanonicalTableName(col);
                                                                                      verifyTableName(tableName);
                                                                                      float nullProbability = getColumnNullProbability(tableName, col);
                                                                                      IsNullFilterOperation operation = new IsNullFilterOperation(col, BigDecimal.valueOf(nullProbability));
                                                                                      if(isornotnull == CompareOperator.IS_NOT_NULL){
                                                                                        operation.setHasNot(true);
                                                                                      }
                                                                                      RESULT = operation;
                                                                                  :}
        ;
null_expr ::=
        IS_NOT_NULL:isnotnull                                                     {: RESULT = isnotnull; :}
        | ISNULL:isnull                                                           {: RESULT = isnull; :}
        ;
